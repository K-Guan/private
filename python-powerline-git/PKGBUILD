_gitname=powerline
_gitbranch=develop
pkgbase=python-powerline-git
pkgname=('python-powerline-git')
pkgdesc='The ultimate statusline/prompt utility'
pkgver=r2391.090cd13
pkgrel=2
epoch=1
url='https://github.com/powerline/powerline'
license=('MIT')
arch=('i686' 'x86_64')
md5sums=('SKIP'
         '1e88fc8a532e2f56a5e0729f6789a33a')
makedepends=('git' 'python2-setuptools' 'python-setuptools' 'python2-sphinx')
install="${_gitname}.install"
source=("${_gitname}::git://github.com/powerline/${_gitname}.git#branch=${_gitbranch}"
        "${install}")

build() {
	cd "${_gitname}"/docs
	make man SPHINXBUILD=sphinx-build2
}

package_generic() {
	# Fonts
	install -dm755 "${pkgdir}/etc/fonts/conf.d"
	install -Dm644 "font/PowerlineSymbols.otf" "${pkgdir}/usr/share/fonts/OTF/PowerlineSymbols.otf"
	install -Dm644 "font/10-powerline-symbols.conf" "${pkgdir}/etc/fonts/conf.avail/10-powerline-symbols.conf"
	ln -s "../conf.avail/10-powerline-symbols.conf" "${pkgdir}/etc/fonts/conf.d/10-powerline-symbols.conf"

	# Vim Plugin
	install -Dm644 "powerline/bindings/vim/plugin/powerline.vim" "${pkgdir}/usr/share/vim/vimfiles/plugin/powerline.vim"

	# Zsh Plugin
	install -Dm644 "powerline/bindings/zsh/powerline.zsh" "${pkgdir}/usr/share/zsh/site-contrib/powerline.zsh"

	# Tmux Configuration
	install -Dm644 "powerline/bindings/tmux/powerline.conf" "${pkgdir}/usr/share/tmux/powerline.conf"

	# License
	install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	# Manpages
	install -dm755 "${pkgdir}/usr/share/man/man1/"
	install -Dm644 "docs/_build/man/"* "${pkgdir}/usr/share/man/man1/"
}

package_python-powerline-git() {
	depends=('python>=3.2')
	optdepends=('python-psutil: improved system information'
	            'python-pygit2: improved git support'
	            'zsh: better shell prompt'
	            'gvim: vim compiled with Python support')
	conflicts=('python2-powerline'
	           'python2-powerline-git'
	           'python-powerline'
	           'otf-powerline-symbols-git')

	cd "${_gitname}"
	python setup.py install --root="${pkgdir}" --optimize=1

	package_generic
}
md5sums=('SKIP'
         '1e88fc8a532e2f56a5e0729f6789a33a')
